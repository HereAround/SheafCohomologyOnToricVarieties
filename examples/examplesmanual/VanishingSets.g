#! @Chapter Computation of vanishing sets

#! @Section GS-cone

#! We compute the GS-cone for a number of examples.

LoadPackage( "ToricVarieties" );

#! @Example
F1 := Fan( [[1],[-1]],[[1],[2]] );
#! <A fan in |R^1>
P1 := ToricVariety( F1 );
#! <A toric variety of dimension 1>
ByASmallerPresentation( ClassGroup( P1 ) );
#! <A free left module of rank 1 on a free generator>
P1xP1 := P1 * P1;
#! <A toric variety of dimension 2 which is a product 
#! of 2 toric varieties>
P2 := ProjectiveSpace( 2 );
#! <A projective toric variety of dimension 2>
ByASmallerPresentation( ClassGroup( P2 ) );
#! <A free left module of rank 1 on a free generator>
P2xP1xP1 := P2*P1*P1;
#! <A toric variety of dimension 4 which is a product 
#! of 3 toric varieties>
P := Polytope( [[ -2,2],[1,2],[2,1],[2,-2],[-2,-2]] );
#! <A polytope in |R^2>
T := ToricVariety( P );
#! <A projective toric variety of dimension 2>
H7 := Fan( [[0,1],[1,0],[0,-1],[-1,7]], [[1,2],[2,3],[3,4],[4,1]] );
#! <A fan in |R^2>
H7 := ToricVariety( H7 );
#! <A toric variety of dimension 2>
H5 := Fan( [[-1,5],[0,1],[1,0],[0,-1]] ,[[1,2],[2,3],[3,4],[4,1]] );
#! <A fan in |R^2>
H5 := ToricVariety( H5 );
#! <A toric variety of dimension 2>
Display( GSCone( P1 ) );
#! A list of 1 hyperplane constraint of a cone in Z^1 - h-constraints are as follows: 
#! [ [  1 ] ]
Display( GSCone( P1xP1 ) );
#! A list of 2 hyperplane constraints of a cone in Z^2 - h-constraints are as follows: 
#! [ [  0,  1 ],
#!   [  1,  0 ] ]
Display( GSCone( P2xP1xP1 ) );
#! A list of 3 hyperplane constraints of a cone in Z^3 - h-constraints are as follows: 
#! [ [  0,  0,  1 ],
#!   [  0,  1,  0 ],
#!   [  1,  0,  0 ] ]
Display( GSCone( T ) );       
#! A list of 6 hyperplane constraints of a cone in Z^3 - h-constraints are as follows: 
#! [ [   0,  0,  1 ],
#!   [   1,  0,  0 ],
#!   [   1,  1,  0 ],
#!   [   0,  1,  0 ],
#!   [   0, -1,  1 ],
#!   [   1,  0,  1 ] ]
Display( GSCone( H7 ) );
#! A list of 3 hyperplane constraints of a cone in Z^2 - h-constraints are as follows: 
#! [ [  0,  1 ],
#!   [  1,  0 ],
#!   [  7,  1 ] ]
Display( GSCone( H5 ) );
#! A list of 3 hyperplane constraints of a cone in Z^2 - h-constraints are as follows: 
#! [ [  0,  1 ],
#!   [  1,  0 ],
#!   [  1,  5 ] ]
#! @EndExample


#! @Section Improved vanishing sets by use of cohomCalg

#! According to cohomCalg (see http://wwwth.mpp.mpg.de/members/bjurke/cohomcalg/ and references on this webpage)
#! the cohomology classes $H^i \left( X_\Sigma, \mathcal{O}_{X_\Sigma} \left( D \right) \right)$ for
#! $D \in \text{Cl} \left( X_\Sigma \right)$ and $X_\Sigma$ a smooth and complete toric variety, can be though of as
#! vector spaces generated by so-called 'rationoms'. A rationom is the quotient of two monoms. For example the
#! following are rationoms:
#! $$ 1, x_1, \frac{x_1}{x_2}, \frac{x_1^3 \cdot x_2}{x_5^7}. $$
#! Let $S$ be the Coxring of the toric variety $X_\Sigma$ in the variables $x_i$. Moreover let 
#! $D \in \text{Cl} \left( X_\Sigma \right)$. Since $X_\Sigma$ is smooth we can therefore identify $D$ with a point
#! in $\mathbb{Z}^r$.

#! Now, to determine $H^i \left( X_\Sigma, \mathcal{O}_{X_\Sigma} \left( D \right) \right)$ cohomCalg proceeds (roughly) as
#! follows:
#! * First it determines which variables $x_i$ appear in the denominators of the rationoms that span $H^j$. This information
#!   is independent of the particular bundle in question.
#! * Finally the actually cohomology classes of $\mathcal{O}_{X_\Sigma} \left( D \right)$ are computed by picking only those
#!   rationoms that transform with weight $D$ under the torus operation.

#! Here is an example to illustrate this strategy:

#! @Example
ContributingDenominators( P1 );
#! [ [ "1" ], [ "u1*u2" ] ]
#! @EndExample

#! The output uses $u_i$ rather than $x_i$ because this is the convention followed by cohomCalg.

#! We now use this information to compute the cohomologies of the structure sheaf of $\mathbb{P}^1$.
#! * $H^0$ is spanned by those rationoms that that only the variable $1$ appearing in their denominator and transform with
#!   weight $0$ under the torus action. It is not hard to verify that the only such rationoms is $1$, so as expected
#!   $H^0 \left( \mathbb{P}^1, \mathcal{O}_{\mathbb{P}^1} \right) \cong k$. 
#! * As for $H^1$, we need to find those rationoms that
#!   have the variables $u_1$ and $u_2$ appear in their denominator and that transform with weight $0$ under the torus action.
#!   Since the only variables in the Coxring are $u_1$ and $u_2$ and (by theory of cohomCalg) the denominator variables are not
#!   allowed to appear in the numerator, we can write such rationoms as $\frac{1}{u_1^a u_2^b}$ with integers $a,b > 1$.
#!   Demanding weight $0$ under the class group shows that no such rationoms exists and thus $H^1$ is the zero vector space.

#! In general the techniques involved are slightly more difficult, but the general philosophy still follows the logic above.

#! Whilst our interest is not in computing line bundle cohomology, but rather determining those regions in the class group
#! such that $H^i$ vanishes for all associated line bundles in this region, we will ask a different question - under what
#! conditions can no such rationoms be found. For example we can ask which line bundles on $\mathbb{P}^1$ have vanishing $H^1$.
#! To answer this we again look at the above denominator contributions and note that the rationom $\frac{1}{u_1^a u_2^b}$
#! transforms with weight $-a - b$. This has to match the degree of the bundle under the condition that the integers $a,b$
#! satisfy $a,b \geq 1$. Therefore we find, that no such solutions exists for line bundles of degree $d \geq -2$.

#! Based on this, we can in fact compute the regions in the class group $\text{Cl} \left( X_\Sigma \right)$ in which all
#! associated bundles have vanishing $H^i$. Here are some examples:

#! @Example
v1 := VanishingSets( P1 );
#! rec( 0 := <A non-full vanishing set in Z^1 for cohomological index 0>, 
#!      1 := <A non-full vanishing set in Z^1 for cohomological index 1> )
Display( v1.0 );
#! A non-full vanishing set in Z^1 for cohomological index 0 formed from the points NOT contained in the following affine semigroup: 
#!
#! A non-trivial affine cone-semigroup in Z^1
#! Offset: [ 0 ]
#! Cone generators: [ [ 1 ] ]
Display( v1.1 );
#! A non-full vanishing set in Z^1 for cohomological index 1 formed from the points NOT contained in the following affine semigroup:
#!
#! A non-trivial affine cone-semigroup in Z^1
#! Offset: [ -2 ]
#! Cone generators: [ [ -1 ] ] 
v2 := VanishingSets( P1xP1 );
#! rec( 0 := <A non-full vanishing set in Z^2 for cohomological index 0>, 
#!      1 := <A non-full vanishing set in Z^2 for cohomological index 1>, 
#!      2 := <A non-full vanishing set in Z^2 for cohomological index 2> )
v3 := VanishingSets( H5 );
#! rec( 0 := <A non-full vanishing set in Z^2 for cohomological index 0>, 
#!      1 := <A non-full vanishing set in Z^2 for cohomological index 1>, 
#!      2 := <A non-full vanishing set in Z^2 for cohomological index 2> )
v4 := VanishingSets( T );
#! rec( 0 := <A non-full vanishing set in Z^3 for cohomological index 0>, 
#!      1 := <A non-full vanishing set in Z^3 for cohomological index 1>, 
#!      2 := <A non-full vanishing set in Z^3 for cohomological index 2> )
Display( v4.2 );
#! A non-full vanishing set in Z^3 for cohomological index 2 formed from the points NOT contained in the following affine semigroup:
#! A non-trivial affine non-cone semigroup in Z^3
#! Offset: [ -1, -1, -2 ]
#! Semigroup generators: [ [ 1, -1, -1 ], [ -1, 0, 0 ], [ -1, 1, 0 ], [ 0\
#! , -1, 0 ], [ 0, 0, -1 ] ]
v5 := VanishingSets( P2 );
#! rec( 0 := <A non-full vanishing set in Z^1 for cohomological index 0>, 
#!      1 := <A full vanishing set in Z^1 for cohomological index 1>, 
#!      2 := <A non-full vanishing set in Z^1 for cohomological index 2> )
Display( v5.1 );
#! A full vanishing set in Z^1 for cohomological index 1
PointContainedInVanishingSet( v1.0, [ 1 ] );
#! false
PointContainedInVanishingSet( v1.0, [ 0 ] );
#! false
PointContainedInVanishingSet( v1.0, [ -1 ] );
#! true
PointContainedInVanishingSet( v1.0, [ -2 ] );
#! true
PointContainedInVanishingSet( v5.1, [ -2 ] );
#! true
PointContainedInVanishingSet( v5.1, [ 3 ] );
#! true

#! @EndExample

#! Finally we are interested in the vanishing set of line bundles which satisfy $H^i = 0$ for all $i > 0$. This is performed
#! by the following commands.

#! @Example
ImprovedVanishingSetForGS( P2 );
#! <A non-full vanishing set in Z^1 for cohomological specification i>0 >
Display( last );
#! A non-full vanishing set in Z^1 for cohomological specification i>0
#! formed from the points NOT contained in the following affine semigroup:
#!
#! A non-trivial affine cone-semigroup in Z^1
#! Offset: [ -3 ]
#! Cone generators: [ [ -1 ] ]

#! @EndExample
